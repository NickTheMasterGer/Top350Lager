<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.0.12">
  <POU Name="HLI3_DoChannelMFunction" Id="{3b1646f4-155c-44d4-ae6b-30083649ca97}">
    <Declaration><![CDATA[FUNCTION_BLOCK HLI3_DoChannelMFunction
VAR_INPUT
	/// Channel number 
	nChanIdx: INT;
	stTechnoAttributeData: St_TechnoAttributeData;
END_VAR
VAR_OUTPUT
	bBusy: BOOL;
END_VAR
VAR
	pChannel: POINTER TO ST_CncChannel;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* ========================================================= *)
(*     FB: HLI_DoChannelMFunction                            *)
(* --------------------------------------------------------- *)
(*   Desc:                                                   *)
(* Author: MK                                                *)
(*   Rev.: 1.1                                               *)
(*   Date: 18.07.2002                                        *)
(*       : 08.04.2005                                        *)
(* ========================================================= *)
(* Rev.:	1.2												 *)
(* Date:	02.10.2010                                   	 *)
(* Author:	HR												 *)
(* Author:	Added support for additional m function number	 *)
(*			via new input variable 'nArg'				 	 *)
(* ========================================================= *)
(* Rev.:	1.3												 *)
(* Date:	23.11.2012                                   	 *)
(* Author:	TV, MiB											 *)
(* Author:	structure St_TechnoAttributeData instead of      *)
(*			simple bool array	 							 *)
(* ========================================================= *)

bBusy := TRUE;
IF NOT HLI3_CheckChannelValid(nChannel := nChanIdx) THEN
	RETURN; (* A b b r u c h *)
END_IF


pChannel := ADR(CNCSystem.Channel[nChanIdx]);

(* Synch M-Functions *)
IF stTechnoAttributeData.nFunctionNumber_r > 0 AND stTechnoAttributeData.nFunctionNumber_r < 1000 THEN
	IF NOT pChannel^.M[stTechnoAttributeData.nFunctionNumber_r].bRes THEN					(* M-Function not set *)
		pChannel^.M[stTechnoAttributeData.nFunctionNumber_r].bState_rw				:= TRUE;	(* set M-Function *)
		pChannel^.M[stTechnoAttributeData.nFunctionNumber_r].nArgument_r		:= stTechnoAttributeData.nArgument_r;
		pChannel^.M[stTechnoAttributeData.nFunctionNumber_r].nBlockNumber_r		:= stTechnoAttributeData.nBlockNumber_r;
		pChannel^.M[stTechnoAttributeData.nFunctionNumber_r].nPrgLineNumber_r	:= stTechnoAttributeData.nPrgLineNumber_r;
		pChannel^.M[stTechnoAttributeData.nFunctionNumber_r].nFunctionNumber_r	:= stTechnoAttributeData.nFunctionNumber_r;
		pChannel^.M[stTechnoAttributeData.nFunctionNumber_r].nTime_r			:= stTechnoAttributeData.nTime_r;		
		pChannel^.M[stTechnoAttributeData.nFunctionNumber_r].bRes 				:= TRUE;	(* set acknowledge *)
		pChannel^.actM_r := CONCAT('M', UDINT_TO_STRING(stTechnoAttributeData.nFunctionNumber_r));
	ELSE
		IF NOT pChannel^.M[stTechnoAttributeData.nFunctionNumber_r].bState_rw THEN				(* wait for quit *)
			pChannel^.M[stTechnoAttributeData.nFunctionNumber_r].bRes 			:= FALSE;	(* quit the ack *)
			pChannel^.M[stTechnoAttributeData.nFunctionNumber_r].nArgument_r	:= 0;		(* reset the argument *)
			pChannel^.M[stTechnoAttributeData.nFunctionNumber_r].nBlockNumber_r		:= 0;
			pChannel^.M[stTechnoAttributeData.nFunctionNumber_r].nPrgLineNumber_r	:= 0;
			pChannel^.M[stTechnoAttributeData.nFunctionNumber_r].nFunctionNumber_r	:= 0;
			pChannel^.M[stTechnoAttributeData.nFunctionNumber_r].nTime_r			:= 0;				
			pChannel^.actM_r :='';
			bBusy 							:= FALSE;	(* quit *)
		END_IF
	END_IF
END_IF


(* Quit M00, M01, M02 , M17, M29, M30 *)
IF 	stTechnoAttributeData.nFunctionNumber_r = 0 OR 
	stTechnoAttributeData.nFunctionNumber_r = 01 OR 
	stTechnoAttributeData.nFunctionNumber_r = 02 OR 
	stTechnoAttributeData.nFunctionNumber_r = 17  OR 
	stTechnoAttributeData.nFunctionNumber_r = 29  OR 
	stTechnoAttributeData.nFunctionNumber_r = 30 THEN
		pChannel^.M[stTechnoAttributeData.nFunctionNumber_r].bState_rw 			:= FALSE;
		pChannel^.M[stTechnoAttributeData.nFunctionNumber_r].nArgument_r	:= 0;
		pChannel^.M[stTechnoAttributeData.nFunctionNumber_r].bRes 			:= FALSE;
		bBusy 							:= FALSE;
END_IF]]></ST>
    </Implementation>
    <ObjectProperties />
  </POU>
</TcPlcObject>